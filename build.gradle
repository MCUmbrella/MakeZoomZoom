buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    coreMod = "xyz.ajp.makezoomzoom.asm.MZZLoadingPlugin"
    def args = [
            "-Dmixin.hotSwap=true",
            "-Dmixin.checks.interfaces=true"
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven { url "http://repo.spongepowered.org/maven" }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade("org.spongepowered:mixin:0.7.8-SNAPSHOT") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    shade "org.jooq:joor-java-8:0.9.7"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'xyz.ajp.makezoomzoom.asm.MZZLoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        attributes "ForceLoadAsMod": "true"
    }
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'LICENSE.txt', 'META-INF/README.txt', 'META-INF/LICENSE.txt', 'META-INF/maven/**'
        }
    }
}

mixin {
    add sourceSets.main, "mixins.mzz.refmap.json"
}

def keystoreFile = new File('makezoomzoom.jks')
def keystorepass = 'nopw'
def keyalias = 'makezoomzoom'

task signJar(dependsOn: 'jar', description: 'Sign jar', group: 'Build') {
    doLast {
        println ":$project.name:${name}"
        def signdir = new File("$buildDir/libs/signed")
        signdir.mkdirs()
        ant.signjar(
                destDir: "${signdir.absolutePath}",
                jar: 'build/libs/*.jar',
                alias: keyalias,
                storetype: 'jks',
                keystore: "${keystoreFile.absolutePath}",
                storepass: keystorepass,
                verbose: true,
                preservelastmodified: true,
                tsaurl: "http://timestamp.digicert.com"
        )
    }
}

build.dependsOn signJar

reobf {
    jar {
        mappingType = "SEARGE"
    }
}